(ns expr.core-test
  (:require [clojure.string :as str]
            [clojure.test :refer [deftest is]]
            [expr.core :as expr]
            [kotoba.expr :as kx]))

(deftest operators-and-leaves
  (is (= "(a + b + c)" (expr/compile [:+ :a :b :c])))
  (is (= "(-x)" (expr/compile [:- :x])))
  (is (= "((a + b) * c)" (expr/compile [:* [:+ :a :b] :c])))
  (is (= "dot(n, l)" (expr/compile [:dot :n :l])))
  (is (= "foo" (expr/compile [:foo])))
  (is (= "x" (expr/compile :x)))
  (is (= "42" (expr/compile 42)))
  (is (= "raw" (expr/compile "raw"))))

(deftest bitwise-and-logical
  (is (= "(p && q)" (expr/compile [:&& :p :q])))
  (is (= "(a >> 2)" (expr/compile [:>> :a 2])))
  (is (= "(a ^ b)" (expr/compile [(keyword "^") :a :b])))
  (is (= "(~m)" (expr/compile [(keyword "~") :m]))))

(deftest customisation-hooks
  (let [snake {:ident (fn [k] (str/replace (name k) "-" "_"))
               :num (fn [n] (str n ".0"))}]
    (is (= "(sun_dir * 2.0)" (expr/compile snake [:* :sun-dir 2])))
    (is (= "max(dot(N, L), 0.0)" (expr/compile snake [:max [:dot :N :L] 0]))))
  (let [ctor {:call (fn [op args] (str (name op) "<f32>(" (str/join ", " args) ")"))}]
    (is (= "vec3<f32>(a, b, c)" (expr/compile ctor [:vec3 :a :b :c]))))
  (let [special {:special (fn [op xs _go] (when (= op :i) (str (first xs))))}]
    (is (= "(7 + x)" (expr/compile special [:+ [:i 7] :x])))))

(deftest compatibility-namespace
  (is (= (expr/compile [:+ :a 1]) (kx/compile [:+ :a 1]))))
